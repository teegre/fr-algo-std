#! /usr/bin/env fralgo

Importer "src/datelib" Alias date

Variable ts en Entier
Variable i en Entier
Variable l en Entier

Procédure Vérifier(horodatage en Numérique, attendu en Chaîne)
  Variable obtenu en Chaîne
  obtenu ← date:Format(horodatage, "%a %F %T %z %Z %j %V")

  Si obtenu <> attendu Alors
    Ecrire "❌ Échec : ", horodatage
    Ecrire "# obtenu  :", obtenu
    Ecrire "# attendu :", attendu
  Sinon
    Ecrire "✅ OK : ", obtenu
  FinSi
FinProcédure

Début
  Ecrire "#################################################################"
  Ecrire "# Tests des fonctions 'datelib:UNIXversLocal' et 'datelib:Format'"
  Ecrire "#################################################################"
  Ecrire ""

  # Cas fixes
  Vérifier(-3600,      "jeu. 01/01/1970 00:00:00 +0100 CET 001 01")
  Vérifier(978303600,  "lun. 01/01/2001 00:00:00 +0100 CET 001 01")
  Vérifier(-315622800, "ven. 01/01/1960 00:00:00 +0100 CET 001 53")
  Vérifier(4102441200, "ven. 01/01/2100 00:00:00 +0100 CET 001 53")
  Vérifier(915145200,  "ven. 01/01/1999 00:00:00 +0100 CET 001 53")
  Vérifier(951692400,  "lun. 28/02/2000 00:00:00 +0100 CET 059 09") # Année bissextile

  Ecrire ""

  # Tests aléatoires
  Pour i ← 1 à 10
    ts ← Entier(Aléa() * 5000000000 - 1000000000)
    l ← Longueur(Chaîne(ts))
    Ecrire "🔄 Test avec", ts, std:RemplirGauche("=>", " ", 10+2-l), date:Format(Numérique(ts), "%a %F %T %z %Z %j %V")
  i Suivant
Fin
